

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "maven-publish"
apply plugin: "java"

dependencies {
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha0'
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha0'

    implementation 'org.jetbrains:annotations:22.0.0'

    implementation project(':WaylanderScheduler')
}

version = "1.0.0"
description = 'WaylanderDatabase'

def classifier = project.ext.isSnapshot ? "SNAPSHOT" : !project.ext.isMain ? "${project.ext.branch}-SNAPSHOT" : ""

publishing {
    repositories {
        maven {
            name = "YotersRepo"
            afterEvaluate {
                def baseUrl = 'https://repo.yoters.com/repository/maven'
                def releasesRepoUrl = "$baseUrl-releases"
                def snapshotsRepoUrl = "$baseUrl-snapshots"
                url = project.ext.isSnapshot || !project.ext.isMain ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = props.get("myNexusUsername", project.properties.get("myNexusUsername") as String)
                    password = props.get("myNexusPassword", project.properties.get("myNexusPassword") as String)
                }
            }
        }
    }
    publications {
        create("${project.name}Maven", MavenPublication) { publication ->
            from components.java
            afterEvaluate {
                publication.version("${project.version}-$classifier")
            }
        }
    }
}

jar {
    archiveClassifier.set(classifier)
}
sourcesJar {
    archiveClassifier.set("sources-$classifier")
}
javadocJar {
    archiveClassifier.set("javadocs-$classifier")
}
shadowJar {
    archiveClassifier.set("all-$classifier")
    relocate 'com.zaxxer', 'me.eduardwayland.mooncraft.waylander.libs.com.zaxxer'
}
